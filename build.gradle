/**
 * Commands:
 *   - gradle build
 *   - gradle test
 *   - gradle assemble
 *   - gradle uploadArchives
 *
 * To execute the 'uploadArchives' task, the following properties must be specified
 * in an external 'gradle.properties' file:
 *   - sonatypeUsername
 *   - sonatypePassword
 */

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

group = 'com.revinate'
version = '3.0.6'
ext.packaging = 'jar'

if (!hasProperty("sonatypeUsername")) {
    ext.sonatypeUsername = null
    ext.sonatypePassword = null
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

dependencies {
    compile 'com.revinate:sendgrid-smtpapi-java:2.0.0'
    compile 'org.apache.httpcomponents:httpcore:4.4.4'
    compile 'org.apache.httpcomponents:httpclient:4.5.1'
    compile 'org.apache.httpcomponents:httpmime:4.5.1'
    compile 'com.fasterxml.jackson.core:jackson-core:2.4.6'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.4.6'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.6'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'commons-io:commons-io:2.4'
}

repositories {
    mavenCentral()
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                artifactId 'sendgrid-java'
                packaging 'jar'
                name 'SendGrid Java Bindings'
                description 'SendGrid Java Bindings'
                url 'https://github.com/revinate/sendgrid-java'

                scm {
                    url 'https://github.com/revinate/sendgrid-java'
                    connection 'scm:git:git://github.com/revinate/sendgrid-java.git'
                    developerConnection 'scm:git:ssh://git@github.com:revinate/sendgrid-java.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'revinate'
                        name 'Revinate, Inc'
                    }
                }
            }
        }
    }
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}
